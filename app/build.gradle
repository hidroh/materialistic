plugins {
    id "me.tatarka.retrolambda" version "3.2.5"
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "io.github.hidroh.materialistic"
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 55
        versionName "2.9"
        buildConfigField "int", "LATEST_RELEASE", "53"
        buildConfigField "String", "GITHUB_TOKEN", "\"\""
        buildConfigField "String", "READABILITY_TOKEN", "\"\""
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro',
                    'proguard-square.pro',
                    'proguard-support.pro',
                    'proguard-rx.pro'
        }
        leak.initWith(buildTypes.debug)
    }

    lintOptions {
        htmlReport false
        xmlReport false
        textReport true
        lintConfig file("${rootProject.getRootDir()}/lint.xml")
        warningsAsErrors true
        abortOnError true
        explainIssues false
        absolutePaths false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    aaptOptions {
        cruncherEnabled = false
    }
}

ext {
    supportVersion = '24.0.0'
    okHttpVersion = '3.3.1'
    daggerVersion = '1.2.5'
    leakCanaryVersion = '1.4-beta2'
    retrofitVersion = '2.1.0'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$supportVersion",
            "com.android.support:recyclerview-v7:$supportVersion",
            "com.android.support:cardview-v7:$supportVersion",
            "com.android.support:design:$supportVersion",
            "com.android.support:preference-v7:$supportVersion",
            "com.android.support:customtabs:$supportVersion",
            "com.squareup.dagger:dagger:$daggerVersion",
            "com.squareup.retrofit2:retrofit:$retrofitVersion",
            "com.squareup.retrofit2:converter-gson:$retrofitVersion",
            "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion",
            "com.squareup.okhttp3:okhttp:$okHttpVersion",
            "com.squareup.okhttp3:logging-interceptor:$okHttpVersion",
            "io.reactivex:rxandroid:1.2.1",
            "io.reactivex:rxjava:1.1.7"
    leakCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    debugCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"
}

apply from: "${rootProject.getRootDir()}/robolectric.gradle"
apply from: "${rootProject.getRootDir()}/jacoco.gradle"
