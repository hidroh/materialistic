apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

task jacocoTestReport(type: JacocoReport) {
    reports {
        xml.enabled true
        csv.enabled false
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.destination file("${buildDir}/reports/coverage")
    }

    classDirectories.from = fileTree(
            dir: "${buildDir}",
            include: [
                    '**/intermediates/javac/debug/classes/**/*.class',
                    '**/tmp/kotlin-classes/debug/**/*.class'
            ])
    sourceDirectories.from = files android.sourceSets.main.java.srcDirs
    executionData.from = files "${buildDir}/jacoco/testDebugUnitTest.exec"

    doLast {
        println "coveralls report has been generated to file://${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        println "jacoco report has been generated to file://${reports.html.destination}/index.html"
    }
}

coveralls {
    saveAsFile = true
    sendToCoveralls = false
}
